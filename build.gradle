buildscript {
    ext.kotlin_version = '1.3.0' // Required for Kotlin integration
    ext.spring_boot_version = '2.1.0.RELEASE'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "gradle.plugin.com.palantir:jacoco-coverage:0.4.0"
    }
}

apply plugin: 'kotlin' // Required for Kotlin integration
apply plugin: "kotlin-spring" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#kotlin-spring-compiler-plugin
apply plugin: 'org.springframework.boot'
apply plugin: "com.palantir.jacoco-coverage"

jar {
    baseName = 'se13-teamcq'
    version = '0.1.0'
}

sourceSets{
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

repositories {
    jcenter()
    google()
    mavenCentral()
}

configurations {
    ktlint
}

test {
    useJUnitPlatform()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // Required for Kotlin integration
    compile "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
    compile 'org.jetbrains.exposed:exposed:0.11.2'
    testCompile "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.1.7'
    testCompile "io.mockk:mockk:1.8.9"
    testCompile 'io.kotlintest:kotlintest-extensions-spring:3.1.10'
    testCompile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // To generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

// To run ktlint during the build (and fail when appropriate)
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5 // FIXME: Increase this threshold as soon as possible!
            }
        }
    }
}

// To run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification
